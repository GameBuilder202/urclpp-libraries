BITS == 32
OUTS 1
OPS 3
REG 3

LLOD R2 SP 1
LLOD R3 SP 2

PSH R2
PSH R3
AND R2 R2 0x7FFFFFFF
AND R3 R3 0x7FFFFFFF
BRG .add_reorder R3 R2
LOD R3 SP
LLOD R2 SP 1
PSH R3
PSH R2
.add_noReorder
AND R2 R2 0x80000000
AND R3 R3 0x80000000
XOR R3 R2 R3
BNZ .add_sub_real R3
LOD R2 SP
LLOD R3 SP 1
CAL .add_getBiasedExponent
MOV R2 R3
MOV R3 R1
CAL .add_getBiasedExponent
SUB R1 R3 R1
LOD R2 SP
LLOD R3 SP 1
PSH R1
CAL .add_getMantissa
MOV R2 R3
MOV R3 R1
CAL .add_getMantissa
MOV R2 R1
POP R1
DEC R1 R1
BRN .add_skpshft R1
BSR R2 R2 R1
PSH R0
BOD .add_rndup R2
.add_rndupret
BSR R2 R2 1
POP R1
ADD R2 R2 R1
.add_skpshft
ADD R3 R3 R2
LOD R2 SP
BGE .add_incExp R3 0x1000000
.add_incExp_Ret
AND R3 R3 0x7FFFFF
AND R1 R2 0xFF800000
ADD R1 R1 R3
POP R0
POP R0
POP R3
POP R2
RET

.add_rndup
STR SP 1
JMP .add_rndupret

.add_incExp
RSH R3 R3
ADD R2 R2 0x00800000
JMP .add_incExp_Ret

.add_reorder
LOD R2 SP
LLOD R3 SP 1
PSH R3
PSH R2
JMP .add_noReorder


.add_getBiasedExponent
BSR R1 R2 23
AND R1 R1 0xFF
RET

.add_getMantissa
PSH R3
BSR R3 R2 23
AND R1 R2 0x7FFFFF
BRZ .add_getMantissa_subnormal R3
ADD R1 R1 0x800000
.add_getMantissa_subnormal
POP R3
RET

.add_sub_real
MOV R1 R2
POP R2
POP R3
PSH R1
PSH R3
PSH R2
CAL .add_getBiasedExponent
MOV R2 R3 //R2 = R3
MOV R3 R1 //R3 = exponent of R2
CAL .add_getBiasedExponent
SUB R1 R3 R1 //R1 = R2e - R3e
POP R2
POP R3
PSH R2
PSH R1
CAL .add_getMantissa
MOV R2 R3 //R2 = R3
MOV R3 R1 //R3 = R2m
CAL .add_getMantissa
MOV R2 R1 //R2 = R3m
POP R1
DEC R1 R1
BRN .add_sub_skpshft R1
BSR R2 R2 R1
PSH R0
BOD .add_sub_rndup R2
.add_sub_rndupret
BSR R2 R2 1 
POP R1
ADD R2 R2 R1
.add_sub_skpshft
SUB R3 R3 R2
POP R2
CAL .add_getBiasedExponent
BRL .add_sub_normalizeloop R3 0x800000
.add_sub_normalizeret
AND R3 R3 0x7FFFFF
BSL R1 R1 23
ADD R3 R1 R3
POP R1
ADD R1 R1 R3
POP R3
POP R2
RET

.add_sub_rndup
STR SP 1
JMP .add_sub_rndupret

.add_sub_normalizeloop
LSH R3 R3
DEC R1 R1
BRZ .add_sub_normalizeret R1
BRL .add_sub_normalizeloop R3 0x800000
JMP .add_sub_normalizeret
